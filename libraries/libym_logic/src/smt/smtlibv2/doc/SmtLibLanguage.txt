			SMT-LIB Ver2.0 言語仕様覚書
							松永　裕介


3.1 Lexicon

Numerals:
	0 | [1-9][0-9]*
	[原文] A <numeral> is the digit 0 or a non-empty sequence of
        digits not starting with 0.

Decimals:
	<numeral> '.' 0* <numeral>
	[原文] A <decimal> is a token of the form <numeral>.0*<numeral>.

Hexadecimals:
	'#x'[0-9a-fA-F]+
	[原文] A <hexadecimal> is a non-empty case-insensitive
	sequence of digits and letters from A to F preceded by the
	(case sensitive) characters #x.

Binaries:
	'#b'[01]+
	[原文] A <binary> is a non-empty sequence of the characters 0
	and 1 preceded by the characters #b.

String literals:
        [原文] A <string> si any sequence of printable ASCII
        characters delimited by double quotes (") and possibly
        containing the C-style escape sequences \" and \\, both of
        which are treated as a single character -- respectively " and
        \. the first escape sequence allows as usual the double quote
        character to appear within a string literal, the second allows
        the backslash character to end a string literal.

Symbols:
	[原文] A <symbol> is either a simple symbol or a quoted
	symbol. The former is any non empty sequence of letters,
	digits and the characters ~ ! @ $ % ^ & * _ - + = < > . ? /
	that does not start with a digit and is not a reserved
	word. The latter is any sequence of printable ASCII characters
	(including space, tab, and line-breaking characters) except
	for the backslash charcter \, that start and ends with | and
	does not otherwise contain |.

Keyword:
	':'[0-9a-zA-Z~!@$%^&*_-+=<>.?/]*
	[原文] A <keyword> is a non-empty sequence of letters, digits,
	and the characters ~ ! ` $ % ^ & * _ - + = < > . ? / preceded
	by the character :. Elements of this category have a special
	use in the language.


3.2 S-expressions

<spec_constant> ::=
	<numeral> | <decimal> | <hexadecimal> | <binary> | <string>

<s_expr> ::=
	<spec_constant> | <symbol> | <keyword> | '(' <s_expr>* ')'


3.3 Identifiers

<identifier> ::=
        <symbol> | '(' '_' <symbol> <numeral>+ ')'


3.4 Attributes

<attribute_value> ::=
        <spec_constant> | <symbol> | '(' <s_expr>* ')'
	要するに <attribute_value> は <s_expr> から <keyword> を除いた
        もの

<attribute> ::=
        <keyword> | <keyword> <attribute_value>


3.5 Sorts

<sort> ::=
        <identifier> | '(' <identifier> <sort>+ ')'


3.6 Terms and Formulas

<qual_identifier> ::=
        <identifier> | '(' 'as' <identifier> <sort> ')'

<var_binding> ::=
        '(' <symbol> <term> ')'

<sorted_var> ::=
        '(' <symbol> <sort> ')'

<term> ::=
        <spec_constant> |
        <qual_identifier> |
	'(' <qual_identifier> <term>+ ')' |
	'(' 'let' '(' <var_binding>+ ')' <term> ')' |
	'(' 'forall' '(' <sorted_var>+ ')' <term> ')' |
	'(' 'exists '(' <sorted_var>+ ')' <term> ')' |
	'(' '!' <term> <attribute>+ ')'


3.7 Theory Declarations

[省略]


3.8 Logic Declarations

[省略]


3.9 Scripts

command ::=
	'(' set-logic <symbol> ')' |
	'(' set-option <option> ')' |
	'(' set-info <attribute> ')' |
	'(' declare-sort <symbol> <numeral> ')' |
	'(' define-sort <symbol> '(' <symbol>* ')' <sort> ')' |
	'(' declare-fun <symbol> '(' <sort>* ')' <sort> ')' |
	'(' define-fun <symbol> '(' <sorted_var>* ')' <sort> <term> ')' |
	'(' push <numeral> ')' |
	'(' pop <numeral> ')' |
	'(' assert <term> ')' |
	'(' check-sat ')' |
	'(' get-assertions ')' |
	'(' get-proof ')' |
	'(' get-unsat-core ')' |
	'(' get-value '(' <term>+ ')' ')' |
	'(' get-assignment ')' |
	'(' get-option <keyword> ')' |
	'(' get-info <info_flag> ')' |
	'(' exit ')'

script ::= <command>*

<b_value> ::= true | false

<option> ::=
	 :print-success <b_value> |
	 :expand-definitions <b_value> |
	 :interactive-mode <b_value> |
	 :produce-proofs <b_value> |
	 :produce-unsat-cores <b_value> |
	 :produce-models <b_value> |
	 :produce-models <b_value> |
	 :produce-assignments <b_value> |
	 :regular-output-channel <string> |
	 :diagnostic-output-channel <string> |
	 :random-seed <numeral> |
	 :verbosity <numeral> |
	 <attribute>

<info_flag> ::=
	 :error-behavior |
	 :name |
	 :authors |
	 :version |
	 :status |
	 :reason-unknown |
	 <keyword> |
	 :all-statistics
